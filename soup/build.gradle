gradle.taskGraph.whenReady { taskGraph ->
    if(taskGraph.hasTask(':soup:assembleRelease')) {
        // Only execute when we are trying to assemble a release build
        def pass = System.console() != null ? System.console().readPassword("\nPlease enter store passphrase: ") : ""
        // readPassword returns a char[] so we need to wrap it into a string, because that's
        // most likely what you need
        pass = new String(pass)
        // Use the pass variable here (so set signing configs here)
        android.signingConfigs.release.storePassword = pass

        def pass2 = System.console() != null ? System.console().readPassword("\nPlease enter key passphrase: ") : ""
        // readPassword returns a char[] so we need to wrap it into a string, because that's
        // most likely what you need
        pass2 = new String(pass2)
        android.signingConfigs.release.keyPassword = pass2
    }
}

// NOTE: Temp fix for Play Services (admob) from: https://code.google.com/p/android/issues/detail?id=75080
tasks.whenTaskAdded { theTask ->
    def taskName = theTask.name.toString()
    if ("testDebug".toString().equals(taskName)) {
        /**
         * Listen for when robolectric adds the 'testDebug' task and when it does, add the -noverify
         * option to that task's jvmArgs.  This allows us to turn off byte code verification when
         * running our unit tests.
         */
        theTask.jvmArgs('-noverify')
    }
}

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.12.0'

    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.1'

    defaultConfig {
        applicationId "com.thunsaker.soup"
        minSdkVersion 9
        targetSdkVersion 19
        versionCode 25
        versionName "2.6.2"
        resConfigs "en", "es", "fr", "pt", "ru"
    }

    signingConfigs {
        debugSigning {
            storeFile file(DEBUG_STORE_FILE)
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        releaseSigning {
            storeFile file(RELEASE_STORE_FILE)
            storePassword ""
            keyAlias RELEASE_KEY_ALIAS
            keyPassword ""
        }
    }

    buildTypes {
        debug {
            zipAlign true
            debuggable true
            runProguard false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debugSigning
            ext.enableCrashlytics = false
        }

        release {
            zipAlign true
            debuggable false
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.releaseSigning
        }
    }
}

dependencies {
    compile project(':foursquare-android-oauth:foursquare-oauth-library')
    compile project(':viewpagerindicator:library')

    compile("com.doomonafireball.betterpickers:library:1.5.2") {
        exclude group: 'com.android.support', module: 'support-v4'
    }

    compile 'com.android.support:support-v4:19.+'
    compile 'com.android.support:appcompat-v7:19.+'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.google.android.gms:play-services:5.0.+'

    compile 'com.koushikdutta.urlimageviewhelper:urlimageviewhelper:1.0.4'
    compile 'com.crashlytics.android:crashlytics:1.1.13'

    compile 'joda-time:joda-time:2.4'

    compile 'com.jakewharton:butterknife:5.1.2'
    compile 'de.greenrobot:eventbus:2.2.0'

    provided 'com.squareup.dagger:dagger-compiler:1.2.1'
    compile 'com.squareup.dagger:dagger:1.2.1'
    compile 'com.squareup.picasso:picasso:2.2.0'
    compile ("com.squareup.retrofit:retrofit:1.5.1") {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    compile 'com.squareup:android-times-square:1.2.1@aar'

    compile 'com.tundem.aboutlibraries:library:3.0.0@aar'
}